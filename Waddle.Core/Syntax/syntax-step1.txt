program :=
    functionDecl* EOF

functionDecl :=
    FUNCTION IDENTIFIER LPAREN parameter* RPAREN (ARROW type)? block

block :=
    LBRACE statement* RBRACE

parameter :=
    IDENTIFIER COLON type

statement :=
    returnStmt
    ifStmt
    assignStmt
    printStmt
    declStmt
    invocationStmt

returnStmt :=
    RETURN expression

ifStmt :=
    IF expression BLOCK

assignStmt :=
    IDENTIFIER EQUAL expression

declStmt :=
    VAR parameter EQUAL expression

printStmt :=
    PRINT LPAREN expression* RPAREN

invocationStmt :=
    invocationExpression

expression :=
    relationalExpression

relationalExpression :=
    termExpression (relationalOperation termExpression)?

relationalOperation :=
    EQ
    NE
    LT
    GT

termExpression :=
    productExpression (termOperation productExpression)*

termOperation :=
    PLUS
    MINUS

productExpression :=
    atom (productOperation atom)*

productOperation :=
    TIMES
    DIVIDE

atom :=
    invocationExpression
    NUMBER
    IDENTIFIER

invocationExpression :=
    ARROW IDENTIFIER LPAREN expression* RPAREN
